import Mathlib.Tactic
import LeanMockingbird.Chapter23
namespace SmullyanMockingbird

/- Chapter 24: Birds That Can Do Arithmetic -/

variable {Bird : Type} [forest : LogicalForest Bird]

-- Other handy abbreviations
abbrev I := forest.I
abbrev V := forest.V

def NumberBird [forest : LogicalForest Bird] : Nat ‚Üí Bird
  | 0 => I
  | n + 1 => V * f * NumberBird n

def Successor (œÉ : Bird) : Prop :=
  ‚àÄ n : Nat, œÉ * @NumberBird Bird forest n = NumberBird (n + 1)

-- By construction, V * f * NumberBird n = NumberBird (n + 1)
theorem lemma_successor :
    @Successor Bird forest (V * f) := by
  rw [Successor]
  intro n
  rfl

-- NumberBird (n + 1) is not equal to NumberBird 0 for any n
theorem thm24_1_1
    (n : Nat) :
    @NumberBird Bird forest (n + 1) ‚â† NumberBird 0 := by
  unfold NumberBird
  by_contra hneg
  let K := forest.K; let hK := forest.hK
  have h : V * forest.f * (NumberBird n) * K = K := by
    rw [hneg, forest.hI]
  rw [hf K I hK forest.hI] at h
  rw [forest.hV, hK] at h
  apply thm22_6 K I hK forest.hnontrivial
  exact h

-- NumberBird (n + 1) = NumberBird (m + 1) implies NumberBird n = NumberBird m
theorem thm24_1_2
    (n m : Nat) :
    @NumberBird Bird forest (n + 1) = NumberBird (m + 1) ‚Üí
    @NumberBird Bird forest n = NumberBird m := by
  intro h
  unfold NumberBird at h
  have h' : ‚àÄ x : Bird, V * f * NumberBird n * x = V * f * NumberBird m * x := by
    intro x
    rw [h]
  specialize h' f
  let K := forest.K; let hK := forest.hK
  rw [forest.hV, forest.hV] at h'
  rw [hf' f, hf' f] at h'
  exact h'

-- NumberBird n ‚â† NumberBird m for all n > m
theorem thm24_1_3
    (n m : Nat) (hnm : n > m) :
    @NumberBird Bird forest n ‚â† NumberBird m := by
  by_contra hneg
  induction n generalizing m with
  | zero =>
    contradiction
  | succ n ih =>
    cases m with
    | zero =>
      apply @thm24_1_1 Bird forest n
      exact hneg
    | succ m =>
      have h' : @NumberBird Bird forest n = NumberBird m := by
        apply thm24_1_2
        exact hneg
      specialize ih m
      have hnm' : n > m := by omega
      specialize ih hnm' h'
      exact ih

-- The Predecessor Bird P
def Predecessor (P : Bird) : Prop :=
  ‚àÄ n : Nat, P * (forest.V * f * NumberBird n) = NumberBird n

-- Existence of a Predecessor bird
theorem thm24_2 :
    ‚àÉ P : Bird, Predecessor P := by
  let T := forest.T; let hT := forest.hT
  let K := forest.K; let hK := forest.hK
  use T * f
  rw [Predecessor]
  intro n
  rw [hT, forest.hV, hf']


-- Zero-tester bird
def ZeroTester (Z : Bird) : Prop :=
  ‚àÄ n : Nat, (n = 0 ‚Üí Z * NumberBird n = t)
            ‚àß (n ‚â† 0 ‚Üí Z * NumberBird n = f)


-- Existence of a Zero-tester bird
theorem thm24_3 :
    ‚àÉ Z : Bird, ZeroTester Z := by
  let T := forest.T; let hT := forest.hT
  let K := forest.K; let hK := forest.hK
  use T * t
  rw [ZeroTester]
  intro n
  cases n with
  | zero =>
    constructor
    ¬∑ intro h0
      rw [hT]
      unfold NumberBird
      rw [forest.hI]
    ¬∑ intro h0
      contradiction
  | succ n =>
    constructor
    ¬∑ intro h0
      contradiction
    ¬∑ intro h0
      rw [hT]
      unfold NumberBird
      rw [forest.hV]
      rw [ht']


theorem thm24_4 :
    ‚àÉ A : Bird, ‚àÄ n : Nat, ‚àÄ x y : Bird,
    (n = 0 ‚Üí A * NumberBird n * x * y = x) ‚àß
    (n ‚â† 0 ‚Üí A * NumberBird n * x * y = y) := by
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  let K := forest.K; let hK := forest.hK
  use Z
  intro n x y
  specialize hZ n
  constructor
  ¬∑ intro hn
    have hZ1 := hZ.1
    specialize hZ1 hn
    rw [hZ1]
    rw [ht']
  ¬∑ intro hn
    have hZ2 := hZ.2
    specialize hZ2 hn
    rw [hZ2]
    rw [hf']



example (S K œÉ P Z : Bird) (hS : Starling S) (hK : Kestrel K) :
    ‚àÄ x y z : Bird,
    S * (S * Z * (K * y)) * (S * (K * œÉ) * (S * (K * (x * y)) * P)) * z =
    Z * z * y * (œÉ * (x * y * (P * z))) := by
  intro x y z
  rw [hS, hS, hK, hS, hK, hS, hK]


example (S K œÉ P Z : Bird) (hS : Starling S) (hK : Kestrel K) :
    ‚àÄ x y z : Bird,
    S
    * (S * (K * S) * (S * (K * (S * Z)) * K))
    * (S * (K * (S * (K * œÉ))) * (S * (S * (K * S) * (S * (K * K) * x)) * (K * P))) * y * z =
    Z * z * y * (œÉ * (x * y * (P * z))) := by
  intro x y z
  rw [hS, hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hK, hS, hS, hK, hS, hK, hS, hK, hK]


example (S K œÉ P Z : Bird) (hS : Starling S) (hK : Kestrel K) :
    ‚àÄ x y z : Bird,
    S * (K * (S * (S * (K * S) * (S * (K * (S * Z)) * K))))
    * (S * (K * (S * (K * (S * (K * œÉ)))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S))) * (S * (K * K)))) * (K * (K * P))))
    * x * y * z =
    Z * z * y * (œÉ * (x * y * (P * z))) := by
  intro x y z
  rw [hS, hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hK, hS,
  hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hK, hK, hK]


-- Addition bird - preparatory lemma
theorem lemma24_5 (Z œÉ P : Bird) :
    ‚àÉ PlusBird : Bird, ‚àÄ n m : Nat,
    PlusBird * NumberBird n * NumberBird m =
    Z * (NumberBird m) * (NumberBird n) *
    (œÉ * (PlusBird * (NumberBird n) * (P * NumberBird m))) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ := S * (K * (S * (S * (K * S) * (S * (K * (S * Z)) * K))))
    * (S * (K * (S * (K * (S * (K * (œÉ))))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S))) * (S * (K * K)))) * (K * (K * P))))
  use Œò * A‚ÇÅ
  intro n m
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hK, hS, hK,
  hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hK, hK, hK]


-- Existence of an addition bird
theorem thm24_5 :
    ‚àÉ PlusBird : Bird, ‚àÄ n m : Nat,
    PlusBird * NumberBird n * NumberBird m = NumberBird (n + m) := by
  let œÉ := forest.V * forest.f
  obtain ‚ü®P, hP‚ü© := @thm24_2 Bird forest
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  obtain ‚ü®PlusBird, hPlusBird‚ü© := lemma24_5 Z œÉ P
  use PlusBird
  intro n m
  induction m with
  | zero =>
    specialize hPlusBird n 0
    have h1 : Z * NumberBird 0 = t := by
      apply (hZ 0).1
      rfl
    rw [hPlusBird, h1, ht']
    rw [Nat.add_zero]
  | succ m ih =>
    specialize hPlusBird n (m + 1)
    rw [hPlusBird]
    have h1 : Z * NumberBird (m + 1) = f := by
      apply (hZ (m + 1)).2
      rw [Nat.add_one]
      omega
    rw [h1, hf']
    unfold œÉ
    have h2 : (PlusBird * NumberBird n * (P * NumberBird (m + 1))) = NumberBird (n + m) := by
      rw [‚Üê lemma_successor, hP]
      exact ih
    rw [h2]
    rw [lemma_successor]
    rw [Nat.add_assoc]



#eval var_eliminate "Times" (var_eliminate "n" (var_eliminate "m" (
  "Z" * "m" * "0" * ("Plus" * ("Times" * "n" * ("P" * "m")) * "n")
)))
-- prints:
/-
S * (K * (S * (K * (S * (S * Z * (K * n0))))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S)))
    * (S * (K * (S * (K * (S * (K * PlusBird)))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S)))
    * (S * (K * K)))) * (K * (K * P)))))) * (K * K))
-/

-- Multiplication bird - preparatory lemma
theorem lemma24_6 (Z n0 P PlusBird : Bird) :
    ‚àÉ TimesBird : Bird, ‚àÄ n m : Nat,
    TimesBird * NumberBird n * NumberBird m =
    Z * (NumberBird m) * (n0) *
    (PlusBird * (TimesBird * (NumberBird n) * (P * (NumberBird m)))
    * NumberBird n) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ :=   S * (K * (S * (K * (S * (S * Z * (K * n0))))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S)))
    * (S * (K * (S * (K * (S * (K * PlusBird)))))
    * (S * (S * (K * S) * (S * (K * (S * (K * S)))
    * (S * (K * K)))) * (K * (K * P)))))) * (K * K))
  use Œò * A‚ÇÅ
  intro n m
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hS,
    hK, hS, hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hK, hK, hK, hK]


-- Existence of a multiplication bird
theorem thm24_6 :
    ‚àÉ TimesBird : Bird, ‚àÄ n m : Nat,
    TimesBird * NumberBird n * NumberBird m = NumberBird (n * m) := by
  obtain ‚ü®P, hP‚ü© := @thm24_2 Bird forest
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  obtain ‚ü®PlusBird, hPlusBird‚ü© := @thm24_5 Bird forest
  obtain ‚ü®TimesBird, hTimesBird‚ü© := @lemma24_6 Bird forest Z (NumberBird 0) P PlusBird
  use TimesBird
  intro n m
  induction m with
  | zero =>
    rw [Nat.mul_zero]
    specialize hTimesBird n 0
    have h1 : Z * NumberBird 0 = t := by
      apply (hZ 0).1
      rfl
    rw [hTimesBird, h1, ht']
  | succ m ih =>
    specialize hTimesBird n (m + 1)
    rw [hTimesBird]
    have h1 : Z * NumberBird (m + 1) = f := by
      apply (hZ (m + 1)).2
      rw [Nat.add_one]
      omega
    rw [h1, hf']
    have h2 : (TimesBird * NumberBird n * (P * NumberBird (m + 1))) = NumberBird (n * m) := by
      rw [‚Üê lemma_successor, hP]
      exact ih
    rw [h2]
    rw [hPlusBird (n * m) n]
    have h3 : n * m + n = n * (m + 1) := by
      linarith
    rw [h3]


#eval var_eliminate "Exp" (var_eliminate "n" (var_eliminate "m" (
  "Z" * "m" * "1" * ("Times" * ("Exp" * "n" * ("P" * "m")) * "n")
)))
-- prints:
/-
  S * (K * (S * (K * (S * (S * Z * (K * 1))))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * (S * (K * (S * (K * Times)))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * K)))) * (K * (K * P)))))) * (K * K))
-/

-- Exponentiating bird - preparatory lemma
theorem lemma24_7 (Z n1 P TimesBird : Bird) :
    ‚àÉ ExpBird : Bird, ‚àÄ n m : Nat,
    ExpBird * NumberBird n * NumberBird m =
    Z * (NumberBird m) * (n1) *
    (TimesBird * (ExpBird * (NumberBird n) * (P * (NumberBird m)))
    * NumberBird n) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ := S * (K * (S * (K * (S * (S * Z * (K * n1))))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * (S * (K * (S * (K * TimesBird)))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * K)))) * (K * (K * P)))))) * (K * K))
  use Œò * A‚ÇÅ
  intro n m
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hS,
    hK, hS, hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hK, hK, hK, hK]

-- Existence of an exponentiating bird
theorem thm24_7 :
    ‚àÉ ExpBird : Bird, ‚àÄ n m : Nat,
    ExpBird * NumberBird n * NumberBird m = NumberBird (n ^ m) := by
  obtain ‚ü®P, hP‚ü© := @thm24_2 Bird forest
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  obtain ‚ü®TimesBird, hTimesBird‚ü© := @thm24_6 Bird forest
  obtain ‚ü®ExpBird, hExpBird‚ü© := @lemma24_7 Bird forest Z (NumberBird 1) P TimesBird
  use ExpBird
  intro n m
  induction m with
  | zero =>
    rw [Nat.pow_zero]
    specialize hExpBird n 0
    have h1 : Z * NumberBird 0 = t := by
      apply (hZ 0).1
      rfl
    rw [hExpBird, h1, ht']
  | succ m ih =>
    specialize hExpBird n (m + 1)
    rw [hExpBird]
    have h1 : Z * NumberBird (m + 1) = f := by
      apply (hZ (m + 1)).2
      rw [Nat.add_one]
      omega
    rw [h1, hf']
    have h2 : (ExpBird * NumberBird n * (P * NumberBird (m + 1))) = NumberBird (n ^ m) := by
      rw [‚Üê lemma_successor, hP]
      exact ih
    rw [h2]
    rw [hTimesBird (n ^ m) n]
    have h3 : n ^ m * n = n ^ (m + 1) := by
      exact rfl
    rw [h3]


/- Preparation for the Finale -/

def Relational (A : Bird) : Prop :=
  ‚àÄ n m : Nat,
  A * NumberBird n * NumberBird m = t ‚à® A * NumberBird n * NumberBird m = f



#eval var_eliminate "A" (var_eliminate "n" (
  "Z" * "n" * "t" * ("ùìù" * ("A" * ("P" * "n")))
))
-- prints:
/-
  S * (K * (S * (S * Z * (K * t))))
  * (S * (K * (S * (K * ùìù)))
  * (S * (S * (K * S) * K) * (K * P)))
-/

-- Even property bird - preparatory lemma
theorem lemma24_8 (Z P t ùìù : Bird) :
    ‚àÉ A : Bird, ‚àÄ n : Nat,
    A * NumberBird n =
    Z * (NumberBird n) * (t) *
    (ùìù * (A * (P * (NumberBird n)))) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ := S * (K * (S * (S * Z * (K * t))))
  * (S * (K * (S * (K * ùìù)))
  * (S * (S * (K * S) * K) * (K * P)))
  use Œò * A‚ÇÅ
  intro n
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hS, hK, hS, hK, hS, hK, hS, hS, hK, hS, hK,
    hK]

-- Existence of an Even property bird
theorem thm24_8 :
    ‚àÉ EvenBird : Bird, ‚àÄ n : Nat,
    EvenBird * NumberBird n = (if n % 2 = 0 then t else f) := by
  obtain ‚ü®P, hP‚ü© := @thm24_2 Bird forest
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  obtain ‚ü®ùìù, hùìù‚ü© := @thm23_1 Bird forest
  obtain ‚ü®EvenBird, hEvenBird‚ü© := @lemma24_8 Bird forest Z P t ùìù
  use EvenBird
  intro n
  induction n with
  | zero =>
    have h1 : Z * NumberBird 0 = t := by
      apply (hZ 0).1
      rfl
    rw [hEvenBird, h1, ht']
    simp
  | succ n ih =>
    specialize hEvenBird (n + 1)
    rw [hEvenBird]
    have h1 : Z * NumberBird (n + 1) = f := by
      apply (hZ (n + 1)).2
      rw [Nat.add_one]
      omega
    rw [h1, hf']
    have h2 : ((P * NumberBird (n + 1))) = NumberBird n := by
      rw [‚Üê lemma_successor, hP]
    rw [h2]
    rw [ih]
    by_cases h : n % 2 = 0
    ¬∑ simp [h]
      have h' : ¬¨ ((n + 1) % 2 = 0) := by omega
      simp [h']
      rw [hùìù.1]
    ¬∑ simp [h]
      have h' : ((n + 1) % 2 = 0) := by omega
      simp [h']
      rw [hùìù.2]


#eval var_eliminate "g" (var_eliminate "n" (var_eliminate "m" (
  "Z" * "n" * "f" * ("Z" * "m" * "t" * ("g" * ("P" * "n") * ("P" * "m")))
)))
-- prints:
/-
  S * (K * (S * (S * (K * S) * (S * (K * K) * (S * Z * (K * f))))))
  * (S * (K * (S * (K * (S * (S * Z * (K * t))))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * (S * (K * K))) * (S * (S * (K * S) * K) * (K * P))))) * (K * (K * P))))
-/

-- "Is greater than" bird - preparatory lemma
theorem lemma24_9 (Z P t f : Bird) :
    ‚àÉ g : Bird, ‚àÄ n m : Nat,
    g * NumberBird n * NumberBird m =
    Z * (NumberBird n) * f * (Z * (NumberBird m) * t *
    (g * (P * (NumberBird n)) * (P * (NumberBird m)))) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ := S * (K * (S * (S * (K * S) * (S * (K * K) * (S * Z * (K * f))))))
  * (S * (K * (S * (K * (S * (S * Z * (K * t))))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S)))
  * (S * (K * (S * (K * K))) * (S * (S * (K * S) * K) * (K * P))))) * (K * (K * P))))
  use Œò * A‚ÇÅ
  intro n m
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hS, hK, hS, hS, hK, hK, hS, hS, hK, hK, hS, hK,
    hS, hS, hS, hS, hK, hK, hS, hS, hK, hS, hK, hS, hS, hK, hS, hK,
    hK, hS, hS, hK, hS, hK, hK, hK, hK]

-- Existence of "Is greater than" bird
theorem thm24_9 :
    ‚àÉ g : Bird, ‚àÄ n m : Nat,
    g * NumberBird n * NumberBird m = (if n > m then t else f) := by
  obtain ‚ü®P, hP‚ü© := @thm24_2 Bird forest
  obtain ‚ü®Z, hZ‚ü© := @thm24_3 Bird forest
  obtain ‚ü®g, hg‚ü© := @lemma24_9 Bird forest Z P t f
  use g
  intro n m
  induction n generalizing m with
  | zero =>
    specialize hg 0 m
    nth_rw 1 [hg]
    have h1 : Z * NumberBird 0 = t := by
      apply (hZ 0).1
      rfl
    rw [h1, ht']
    simp
  | succ n ih =>
    specialize hg (n + 1) m
    rw [hg]
    have h1 : Z * NumberBird (n + 1) = f := by
      apply (hZ (n + 1)).2
      rw [Nat.add_one]
      omega
    rw [h1, hf']
    cases m with
    | zero =>
      have h0 : Z * NumberBird 0 = t := by
        apply (hZ 0).1
        rfl
      simp [h0]
      rw [ht']
    | succ m =>
      have h0 : Z * NumberBird (m + 1) = f := by
        apply (hZ (m + 1)).2
        omega
      rw [h0, hf']
      have h2 : ((P * NumberBird (n + 1))) = NumberBird n := by
        rw [‚Üê lemma_successor, hP]
      have h3 : ((P * NumberBird (m + 1))) = NumberBird m := by
        rw [‚Üê lemma_successor, hP]
      rw [h2, h3]
      specialize ih m
      rw [ih]
      simp


def Regular (A : Bird) : Prop :=
  ‚àÄ n : Nat, ‚àÉ m : Nat,
  A * NumberBird n * NumberBird m = t

open Classical in
def MinimizerOf (A' A : Bird) (hreg : Regular A) : Prop :=
  ‚àÄ n : ‚Ñï,
  A' * NumberBird n = NumberBird (Nat.find (hreg n))

#eval var_eliminate "A‚ÇÅ" (var_eliminate "n" (var_eliminate "m" (
  "A" * "n" * "m" * "m" * ("A‚ÇÅ" * "n" * ("œÉ" * "m"))
)))
-- prints:
/-
  S * (K * (S * (S * (K * S) * (S * (S * (K * S) * A) * (K * I)))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S))) * (S * (K * K)))) * (K * (K * œÉ)))
-/

-- A‚ÇÅ bird - preparatory lemma
theorem lemma24_10_1 (A œÉ : Bird) :
    ‚àÉ A‚ÇÅ : Bird, ‚àÄ n m : Nat,
    A‚ÇÅ * NumberBird n * NumberBird m =
    A * (NumberBird n) * (NumberBird m) * (NumberBird m) *
    (A‚ÇÅ * (NumberBird n) * (œÉ * (NumberBird m))) := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let Œò := forest.Œò; let hŒò := forest.hŒò
  let A‚ÇÅ := S * (K * (S * (S * (K * S) * (S * (S * (K * S) * A) * (K * I)))))
  * (S * (S * (K * S) * (S * (K * (S * (K * S))) * (S * (K * K)))) * (K * (K * œÉ)))
  use Œò * A‚ÇÅ
  intro n m
  rw [SageBird] at hŒò
  specialize hŒò A‚ÇÅ (Œò * A‚ÇÅ) rfl
  rw [FondOf] at hŒò
  nth_rw 1 [‚Üê hŒò]
  dsimp [A‚ÇÅ]
  nth_rw 1 [hS, hK, hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hS, hK,
    hS, hS, hK, hS, hK, hS, hS, hK, hK, hK, hK, forest.hI]

-- Existence of A‚ÇÅ for any A
theorem lemma24_10_2 (A : Bird) :
    ‚àÉ A‚ÇÅ : Bird, ‚àÄ n m : Nat,
    (A * NumberBird n * NumberBird m = f ‚Üí
      A‚ÇÅ * NumberBird n * NumberBird m = A‚ÇÅ * NumberBird n * (V * f * NumberBird m))
      ‚àß
    (A * NumberBird n * NumberBird m = t ‚Üí
      A‚ÇÅ * NumberBird n * NumberBird m = NumberBird m) := by
  let œÉ : Bird := V * f
  have hœÉ : Successor œÉ := lemma_successor
  obtain ‚ü®A‚ÇÅ, hA‚ÇÅ‚ü© := @lemma24_10_1 Bird forest A œÉ
  use A‚ÇÅ
  unfold œÉ at hœÉ
  intro n m
  constructor
  ¬∑ intro hA
    rw [hœÉ]
    specialize hA‚ÇÅ n m
    rw [hA‚ÇÅ, hA, hf', hœÉ]
  ¬∑ intro hA
    rw [hA‚ÇÅ, hA, ht']



-- Existence of a Minimizer bird A' for any regular relational A
open Classical in
theorem thm24_10 (A : Bird) (hreg1 : Relational A) (hreg2 : Regular A) :
    ‚àÉ A' : Bird, MinimizerOf A' A hreg2 := by
  let C := forest.C; let hC := forest.hC
  let n0 : Bird := NumberBird 0
  obtain ‚ü®A‚ÇÅ, hA‚ÇÅ‚ü© := lemma24_10_2 A
  use C * A‚ÇÅ * n0
  intro n
  rw [hC]
  specialize hreg2 n
  let k : Nat := Nat.find hreg2
  have k_good := Nat.find_spec hreg2
  have h : ‚àÄ k' < k, A‚ÇÅ * NumberBird n * NumberBird k' =
    A‚ÇÅ * NumberBird n * (NumberBird (k' + 1)) := by
      intro k' hk'
      have h0 : ¬¨ (A * NumberBird n * NumberBird k' = t) := by
        apply Nat.find_min hreg2 hk'
      rw [Relational] at hreg1
      specialize hreg1 n k'
      have h1 : A * NumberBird n * NumberBird k' = f := by
        apply hreg1.resolve_left h0
      specialize hA‚ÇÅ n k'
      rw [lemma_successor] at hA‚ÇÅ
      apply hA‚ÇÅ.1
      exact h1
  have h_ind : ‚àÄ j : Nat, (A * NumberBird n * NumberBird k = t ‚àß
      ‚àÄ k' < j, A‚ÇÅ * NumberBird n * NumberBird k' =
      A‚ÇÅ * NumberBird n * (NumberBird (k' + 1))) ‚Üí
      A‚ÇÅ * NumberBird n * NumberBird 0 = A‚ÇÅ * NumberBird n * NumberBird j := by
    intro k
    induction k with
    | zero =>
      intro _
      rfl
    | succ k ih =>
      intro h2
      have h_prev : ‚àÄ k' < k, A‚ÇÅ * NumberBird n * NumberBird k' =
          A‚ÇÅ * NumberBird n * NumberBird (k' + 1) :=
        fun k' hk' => h2.2 k' (Nat.lt_succ_of_lt hk')
      specialize ih ‚ü®h2.1, h_prev‚ü©
      have h_step : A‚ÇÅ * NumberBird n * NumberBird k
          = A‚ÇÅ * NumberBird n * NumberBird (k + 1) :=   by
        apply h2.2 k
        omega
      rw [‚Üê h_step]
      exact ih
  specialize h_ind k ‚ü®k_good, h‚ü©
  rw [h_ind]
  apply (hA‚ÇÅ n k).2
  exact k_good


-- Length function and its well-posedness
theorem lemma24_11_bound (n : ‚Ñï) : 10^n > n := by
  induction n with
  | zero =>
    -- Base case: 0 < 10^0
    simp
  | succ k ih =>
    -- Inductive step: show k + 1 < 10^(k + 1)
    calc
      k + 1 ‚â§ 10^k := by
        -- Since 10^k > k, and they are naturals, 10^k >= k+1
        -- Except for cases where 10^k is very small (but 10^k is at least 1)
        exact ih -- (Actually, ih is k < 10^k, which implies k + 1 <= 10^k)
      _ < 10 * 10^k := by
        -- 10^k < 10 * 10^k is true because 10^k >= 1
        omega
      _ = 10^(k + 1) := by
        apply Eq.symm
        rw [Nat.pow_add_one]
        omega

theorem lemma24_11_exist (n : ‚Ñï) : ‚àÉ k : ‚Ñï, 10^k > n := by
  use n
  exact lemma24_11_bound n

def LengthFunction (n : ‚Ñï) : ‚Ñï :=
  Nat.find (lemma24_11_exist n)

def LengthBird (‚Ñì : Bird) : Prop :=
  ‚àÄ n : ‚Ñï, ‚Ñì * NumberBird n = NumberBird (LengthFunction n)

-- Existence of a "length measurer" bird
open Classical in
theorem thm24_11 :
    ‚àÉ ‚Ñì : Bird, LengthBird ‚Ñì := by
  obtain ‚ü®g, hg‚ü© := @thm24_9 Bird forest
  obtain ‚ü®ExpBird, hExpBird‚ü© := @thm24_7 Bird forest
  let B := forest.B; let hB := forest.hB
  let C := forest.C; let hC := forest.hC
  let A := C * (B * g * (ExpBird * NumberBird 10))
  have h1 : ‚àÄ n m : ‚Ñï, 10 ^ n > m ‚Üî A * NumberBird m * NumberBird n = t := by
    intro n m  --hnm
    constructor
    ¬∑ intro hnm
      unfold A
      rw [hC, hB, hExpBird, hg]
      simp [hnm]
    ¬∑ intro hA
      rw [hC, hB, hExpBird, hg] at hA
      by_contra hneg
      rw [if_neg hneg] at hA
      unfold f t at hA
      let K := forest.K; let hK := forest.hK
      let I := forest.I; let hI := forest.hI
      rw [ht K hK] at hA
      rw [hf K I hK hI] at hA
      apply thm22_6 K I hK forest.hnontrivial
      exact hA
  have h2 : ‚àÄ n m : ‚Ñï, ¬¨ 10 ^ n > m ‚Üí A * NumberBird m  * NumberBird n = f := by
    intro n m hnm
    rw [hC, hB, hExpBird, hg]
    simp [hnm]
  have hreg1 : Relational A := by
    rw [Relational]
    intro n m
    by_cases h_case : 10 ^ m > n
    constructor
    ¬∑ apply (h1 m n).1 h_case
    ¬∑ right
      apply h2 m n h_case
  have hreg2 : Regular A := by
    rw [Regular]
    intro n
    obtain ‚ü®k, hk‚ü© := lemma24_11_exist n
    use k
    specialize h1 k n
    tauto
  obtain ‚ü®‚Ñì, h‚Ñì‚ü© := @thm24_10 Bird forest A hreg1 hreg2
  use ‚Ñì
  rw [LengthBird]
  rw [MinimizerOf] at h‚Ñì
  intro n
  specialize h‚Ñì n
  rw [LengthFunction]
  have h_equiv : ‚àÄ k, 10^k > n ‚Üî A * NumberBird n * NumberBird k = t := by
    intro k
    exact h1 k n
  rw [h‚Ñì]
  congr 1
  obtain ‚ü®k, hk‚ü© := hreg2 n
  apply Nat.find_congr hk
  intro n1 hn1
  specialize h1 n1 n
  apply Iff.symm
  exact h1


def ConcatFunction (a b : ‚Ñï) : ‚Ñï :=
  a * (10 ^ LengthFunction b) + b

def ConcatBird (A : Bird) : Prop :=
  ‚àÄ n m : ‚Ñï, A * NumberBird n * NumberBird m = NumberBird (ConcatFunction n m)

#eval var_eliminate "x" (var_eliminate "y" (
  "Plus" * ("Times" * "x" * ("Exp" * "10" * ("‚Ñì" * "y"))) * "y"
))
-- prints:
/-
  S * (S * (K * S) * (S * (K * (S * (K * Plus)))
  * (S * (S * (K * S) * (S * (K * K) * Times))
  * (K * (S * (K * (Exp * 10)) * ‚Ñì))))) * (K * I)
-/

-- Existence of a Concatenation bird
theorem thm24_12 :
    ‚àÉ A : Bird, ConcatBird A := by
  let K := forest.K; let hK := forest.hK
  let S := forest.S; let hS := forest.hS
  let hI := forest.hI
  let n10 : Bird := NumberBird 10
  obtain ‚ü®PlusBird, hPlusBird‚ü© := @thm24_5 Bird forest
  obtain ‚ü®TimesBird, hTimesBird‚ü© := @thm24_6 Bird forest
  obtain ‚ü®ExpBird, hExpBird‚ü© := @thm24_7 Bird forest
  obtain ‚ü®‚Ñì, h‚Ñì‚ü© := @thm24_11 Bird forest
  use S * (S * (K * S) * (S * (K * (S * (K * PlusBird)))
    * (S * (S * (K * S) * (S * (K * K) * TimesBird))
    * (K * (S * (K * (ExpBird * n10)) * ‚Ñì))))) * (K * I)
  rw [ConcatBird]
  intro n m
  rw [hS, hS, hK, hS, hS, hK, hS, hK, hS, hS, hK, hS, hS, hK, hK,
    hK, hS, hK, hK, hI]
  rw [h‚Ñì, hExpBird, hTimesBird, hPlusBird]
  simp [ConcatFunction]



end SmullyanMockingbird
